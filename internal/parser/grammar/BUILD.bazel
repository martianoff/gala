load("@rules_go//go:def.bzl", "go_library")

genrule(
    name = "gala_grammar_go",
    srcs = ["gala.g4"],
    outs = [
        "gala_base_listener.go",
        "gala_lexer.go",
        "gala_listener.go",
        "gala_parser.go",
    ],
    cmd = """
        set -e && \
        ANTLR_OUT=antlr_tmp_$$$$ && \
        mkdir -p $$ANTLR_OUT && \
        "$(JAVA)" -jar $(location @antlr4_tool//jar) -Dlanguage=Go -package grammar -o $$ANTLR_OUT $(location gala.g4) && \
        find $$ANTLR_OUT -name '*.go' -type f | while read f; do \
            sed 's|github.com/antlr/antlr4/runtime/Go/antlr|github.com/antlr4-go/antlr/v4|g' "$$f" > "$(@D)/$$(basename $$f)"; \
        done && \
        rm -rf $$ANTLR_OUT
    """,
    toolchains = ["@bazel_tools//tools/jdk:current_host_java_runtime"],
    tools = ["@antlr4_tool//jar"],
)

go_library(
    name = "grammar",
    srcs = [
        "gala_base_listener.go",
        "gala_lexer.go",
        "gala_listener.go",
        "gala_parser.go",
    ],
    importpath = "martianoff/gala/internal/parser/grammar",
    visibility = ["//visibility:public"],
    deps = ["@com_github_antlr4_go_antlr_v4//:antlr"],  # keep
)

exports_files(["gala.g4"])
