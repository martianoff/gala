load("@rules_go//go:def.bzl", "go_library")
load("@rules_java//java:defs.bzl", "java_binary")

java_binary(
    name = "antlr4",
    main_class = "org.antlr.v4.Tool",
    runtime_deps = ["@antlr4_tool//jar"],
)

genrule(
    name = "gala_grammar_go",
    srcs = ["gala.g4"],
    outs = [
        "gala_base_listener.go",
        "gala_lexer.go",
        "gala_listener.go",
        "gala_parser.go",
    ],
    cmd = """
        set -e && \
        ANTLR_OUT=antlr_tmp_$$$$ && \
        mkdir -p $$ANTLR_OUT && \
        $(location :antlr4) -Dlanguage=Go -package grammar -o $$ANTLR_OUT $(location gala.g4) && \
        for f in $$ANTLR_OUT/*.go; do \
            sed 's|github.com/antlr/antlr4/runtime/Go/antlr|github.com/antlr4-go/antlr/v4|g' "$$f" > "$(@D)/$$(basename $$f)"; \
        done && \
        rm -rf $$ANTLR_OUT
    """,
    tools = [":antlr4"],
)

go_library(
    name = "grammar",
    srcs = [
        "gala_base_listener.go",
        "gala_lexer.go",
        "gala_listener.go",
        "gala_parser.go",
    ],
    importpath = "martianoff/gala/internal/parser/grammar",
    visibility = ["//visibility:public"],
    deps = ["@com_github_antlr4_go_antlr_v4//:antlr"],  # keep
)

exports_files(["gala.g4"])
